*************	P	R	O	G	R	A	M		H	E	A	D	E	R	*****************
*****************************************************************************************
*																						*
*	PROGRAM:	4.2 - Subsetting Data.sas                                               *
*	PURPOSE:	Illustrate material in Chapter 4.2                                      *
*	AUTHOR:		Nelson															*
*	CREATED:	2017-10-26																*
*	                                                                                    *
*	COURSE:		BIOS 6680 - Data Management Using SAS                                   *
*	DATA USED:	SASHelp.(Cars, Baseball, Class), In-Stream                              *
*	SOFTWARE:	SAS (r) Proprietary Software 9.4 (TS1M4)								*
*	MODIFIED:	DATE		BY	REASON													*
*				----------	---	-------------------------------------------------------	*
*               2018-10-29  PJB Added TOC & Comprehension Checks                        *
*               2019-09-23  SMN Minor edits                                             *
*	                                                                                    *
*   CONTENTS:                                                                           *
*   	Section 4.2.1 - Restricting Observations Read Into a Data Set                   *
*   	Section 4.2.2 - Conditionally Restricting Data Set Observations                 *
*   	Section 4.2.3 - Subsetting Variables In a Data Set                              *
*   	Section 4.2.4 - Additional Material                                             *
*   	Section 4.2.5 - Solutions                                                       *
*	                                                                                    *
*****************************************************************************************
***********************************************************************************; RUN;


*   Instructions:
    1)  Change the path in the %LET statement to the location of the BIOS 6680 course root folder
    2)  Submit the %LET and LIBNAME statements below   *;
*	Note:  Forward slashes are used for portability across operating environments   *;

%LET	CourseRoot = C:/Dropbox/2 - Education/7 - Teaching/1 - SAS/BIOS 6680 - Data Management Using SAS/4 - Projects/Programming Project;
LIBNAME CanImpt    "&CourseRoot/Cancer RCT/Data/2_Import";




*	SECTION 4.2.1 - RESTRICTING OBSERVATIONS READ INTO A DATA SET   *; RUN;

*	The SASHelp.Cars data set will be used to illustrate some concepts
	The data set has 428 observations
	The data set is sorted by Make (Acura -> Volvo), then Type   *;

PROC PRINT DATA = SASHelp.Cars;
	RUN;


*	Illustration 1 - Using the OBS= Data Set Option   *; RUN;

*	The OBS= data set option specifies the last observation to process
		(not necessarily the number being processed)
	This option is used with the data set that is being read (not the data set being created)   *;
DATA	WORK.Illus;
	SET	SASHelp.Cars	(OBS = 10);
	RUN;

PROC PRINT DATA = WORK.Illus;
	RUN;


*	Illustration 2 - Using the FIRSTOBS= Data Set Option   *; RUN;

*	The FIRSTOBS= data set option specifies the first observation to process   *;
DATA	WORK.Illus;
	SET	SASHelp.Cars	(FIRSTOBS = 400);
	RUN;

PROC PRINT DATA = WORK.Illus;
	RUN;


*	Illustration 3 - Using Both the FIRSTOBS= and OBS= Data Set Options   *; RUN;

DATA	WORK.Illus;
	SET	SASHelp.Cars	(FIRSTOBS = 20	OBS = 30);
	RUN;

PROC PRINT DATA = WORK.Illus;
	RUN;


*	Illustration 4 - Checking the Values of the OBS= and FIRSTOBS= System Options   *; RUN;

*	Recall:  PROC OPTIONS can be used to check the setting for any option specified with the OPTION= option   *; 
PROC OPTIONS	OPTION = OBS;
	RUN;

*	Multiple options can be checked if grouped within parentheses   *;
PROC OPTIONS	OPTION = (FIRSTOBS OBS);
	RUN;


*	Illustration 5 - Using the OBS= System Option   *; RUN;

*	Setting a system option obviates the need to set a data set option
	Recall, however, that the system option will remain in effect until
		1)	It is changed, or
		2)	The SAS session is ended   *;
OPTIONS	OBS = 5;

DATA	WORK.Illus;
	SET	SASHelp.Cars;
	RUN;

PROC PRINT DATA = WORK.Illus;
	RUN;


*	Illustration 6 - Using the FIRSTOBS= and OBS= System Options   *; RUN;

OPTIONS	FIRSTOBS = 300	OBS = 305;

DATA	WORK.Illus;
	SET	SASHelp.Cars;
	RUN;
*	Note: Using the current system options I can't print the 6 observations
			A WARNING message will be written to the log window   *;


*	Illustration 7 - Data Set Options Override System Options   *; RUN;

DATA	WORK.Illus;
	SET	SASHelp.Cars	(FIRSTOBS = 45	OBS = 47);
	RUN;

*	NB:  Be sure to reset the options back to their default values!   *;
OPTIONS	FIRSTOBS = 1	OBS = MAX;

PROC PRINT DATA = WORK.Illus;
	RUN;


*	Illustration 8 - Using the Automatic Variable _N_   *; RUN;

*	Recall:  _N_ counts the iterations of the DATA step (it does NOT count observation numbers!)
	NB:  This is less efficient than the OBS= data set option
			b/c SAS still processes every observation   *;
DATA	WORK.Illus;
	SET	SASHelp.Cars;
	IF	85 < _N_ < 95;
	RUN;

PROC PRINT DATA = WORK.Illus;
	RUN;


*	Illustration 9 - Understanding _N_ in the presence of the FIRSTOBS= and OBS= options   *; RUN;

*	Again, OBS= and FIRSTOBS= specify which observations to process,
		_N_ counts the iterations of the DATA step   *;
DATA	WORK.Illus;
	SET	SASHelp.Cars	(FIRSTOBS = 200	OBS = 210);
	PUTLOG Make= Model= _N_=; * Observe the values of _N_! *;
	RUN;

PROC PRINT DATA = WORK.Illus;
	RUN;


* ----------------------------- *
|   Comprehension Check 4.2.1   |
* ----------------------------- *
	Question:  Which observations will be read into the data set?   *;
DATA	WORK.Illus;
	SET	SASHelp.Cars	(FIRSTOBS = 301	OBS = 350);
	IF	_N_ >= 40;
	RUN;




*	SECTION 4.2.2 - CONDITIONALLY RESTRICTING DATA SET OBSERVATIONS   *; RUN;


*	Illustration 1 - Using the Subsetting IF Statement   *; RUN;

*	The Subsetting IF statement continues processing only observations for which
		the condition specified in the expression following 'IF' is true

	The expression after the 'IF' keyword is evaluated.  When the expression is:
		TRUE:  SAS continues processing the observation
		FALSE:  SAS 1) Stops processing the observation
					2) Returns to the start of the DATA step
					3) Loads the next observation into the PDV   *;
DATA	WORK.Illus;
	SET	SASHelp.Cars;
	IF	Origin = 'Asia';
	RUN;

PROC PRINT DATA = WORK.Illus;
	RUN;


*	Illustration 2 - Multiple Conditions May be Specified   *; RUN;

*	Use a compound expression to specify the multiple conditions
	The expressions may be joined using the logical operators AND or OR
		AND:  Evaluated as true if both conditions are true
		OR:   Evaluated as true if either condition is true *;
DATA	WORK.Illus;
	SET	SASHelp.Cars;
	IF	Origin 		= 'Asia'	AND
		DriveTrain	= 'Rear'	;
	RUN;

PROC PRINT DATA = WORK.Illus;
	RUN;
*	Although multiple IF statement could produce the same results, the code and processing
		are less efficient because SAS must process multiple statements

	Example using the OR logical operator   *;
DATA	WORK.Illus;
	SET	SASHelp.Cars;
	IF	Origin 		= 'Asia'	OR
		DriveTrain	= 'Rear'	;
	RUN;

PROC PRINT DATA = WORK.Illus;
	RUN;


*	Illustration 3 - The DELETE Statement   *; RUN;

*	The DELETE statement instructs SAS to stop processing the current observation
		and return to the top of the DATA step
		(the observation is not sent to the output data set)   *;
DATA	WORK.Illus;
	SET	SASHelp.Cars;
	DELETE;
	RUN;
*	Comment:  Not very useful unless it's used conditionally :)   *;


*	Illustration 4 - Using DELETE Conditionally   *; RUN;

*	Use the DELETE statement conditionally to not include observations that don't
		meet the specified condition(s)   *;
DATA	WORK.Illus;
	SET	SASHelp.Cars;
	IF	Origin 		= 'Asia'	OR
		DriveTrain	= 'Rear'	THEN DELETE;
	RUN;

PROC PRINT DATA = WORK.Illus;
	RUN;


*	Illustration 5 - Showing Several Methods to Achieve a Desired Subset   *; RUN;

*	Task:  Create a data set containing all players who played 3rd base in the SASHelp.Baseball data set
		To accomplish this we'll be using the 'Position' variable
	First, let's look at the position values we want to keep   *;
PROC FREQ
		DATA	= SASHelp.Baseball
		NLEVELS;
	TABLES	Position	/	MISSING NOPERCENT;
	RUN;

*	Method 1:  Use a subsetting IF statement with a compound expression to keep all values containing 3rd base   *;
DATA	WORK.ThirdBasemen;
	SET	SASHelp.Baseball;
	IF	Position = '13'	OR	Position = '23'	OR	Position = '32'	OR	Position = '3B'	OR
		Position = '3O'	OR	Position = '3S'	OR	Position = 'S3'	;
	RUN;

*	Method 2:  Use a subsetting IF statement and the IN operator to keep all values containing 3rd base
	The IN operator is used to compare to a list of values
	It is a shortcut to writing several expressions joined by the OR operator
		Values must be separated by commas or spaces (or both)   *;
DATA	WORK.ThirdBasemen;
	SET	SASHelp.Baseball;
	IF	Position IN('13', '23', '32', '3B', '3O', '3S', 'S3'); * I chose to use both commas and spaces *;
	RUN;

*	Method 3:  Conditionally deleting observations (values not including 3rd base)   *;
DATA	WORK.ThirdBasemen;
	SET	SASHelp.Baseball;
	IF	Position IN('1B', '1O', '2B', '2S', 'C', 'CD', 'CF', 'CS', 'DH',
					'DO', 'LF', 'O1', 'OD', 'OF', 'OS', 'RF', 'SS', 'UT')	THEN DELETE;
	RUN;

*	Method 4:  More efficient coding for conditionally deleting observations
				(values not including 3rd base)
	Note:  The ~ symbol is equivalent to the word "NOT"   *;
DATA	WORK.ThirdBasemen;
	SET	SASHelp.Baseball;
	IF	Position ~IN('13', '23', '32', '3B', '3O', '3S', 'S3')	THEN DELETE;
	RUN;

*	Method 5:  Using the FINDC function to keep all observations with a value containing 3rd base
	Recall:  The FINDC function returns the position SAS first finds the desired character,
				and returns '0' if the character is not found   *;
DATA	WORK.ThirdBasemen;
	SET	SASHelp.Baseball;
	IF	FINDC(Position, '3') > 0;
	RUN;

PROC PRINT DATA = WORK.ThirdBasemen;
	VAR	Name Team Position;
	RUN;
*	Comment:  Method 5 processes most efficiently   *;


*	Illustration 6 - Using a WHERE Statement   *; RUN;

*	The WHERE statement selects observations which meet a particular condition   *;
DATA	WORK.ThirdBasemen;
	SET	SASHelp.Baseball;
	WHERE	FINDC(Position, '3') > 0;
	RUN;

PROC PRINT DATA = WORK.ThirdBasemen;
	VAR	Name Team Position;
	RUN;


*	Illustration 7 - Using the WHERE= Data Set Option   *; RUN;

*	The WHERE= data set option specifies the condition(s) which must be met to select
		observations from a data set   *;
DATA	WORK.ThirdBasemen;
	SET	SASHelp.Baseball	(	WHERE = (	FINDC(Position, '3') > 0	)
							);
	RUN;

PROC PRINT DATA = WORK.ThirdBasemen;
	VAR	Name Team Position;
	RUN;

*	Comment:  The WHERE statement and WHERE= data set option perform equally well.
	When we work with multiple data sets, it will be useful to know:
		1) If you want the same condition to apply to all data sets, it is easier to code
				a single WHERE statement
		2) If different conditions should be applied to different data sets, use
				WHERE= data set options for each data set	*;


*	Illustration 8 - Specifying Multiple Conditions   *; RUN;

*	Method 1:  Using a compound WHERE statement   *;
DATA	WORK.Illus;
	SET	SASHelp.Cars;
	WHERE	Origin		= 'Asia'	AND
			DriveTrain	= 'Rear';
	RUN;

PROC PRINT DATA = WORK.Illus;
	RUN;

*	Method 2:  Using WHERE ALSO
	Although not shown here, there are times when the data set may need to be filtered
		by another condition later in the DATA step
		In that situation WHERE ALSO should be used   *;
DATA	WORK.Illus;
	SET	SASHelp.Cars;
	WHERE		Origin		= 'Asia';
	WHERE ALSO	DriveTrain	= 'Rear';
	RUN;

PROC PRINT DATA = WORK.Illus;
	RUN;

*	Method 3:  Using WHERE SAME AND
	Same comment as above   *;
DATA	WORK.Illus;
	SET	SASHelp.Cars;
	WHERE			Origin		= 'Asia';
	WHERE SAME AND	DriveTrain	= 'Rear';
	RUN;

PROC PRINT DATA = WORK.Illus;
	RUN;

*	NB:  Don't use 2 WHERE statements as shown below!
	The first WHERE statement will be replaced by the 2nd WHERE statement   *;
DATA	WORK.Illus;
	SET	SASHelp.Cars;
	WHERE	Origin		= 'Asia';
	WHERE 	DriveTrain	= 'Rear';
	RUN;

PROC PRINT DATA = WORK.Illus;
	RUN;


*	Illustration 9 - Comparing IF vs WHERE:  How SAS Processes the PDV   *; RUN;

*	The IF statement tests the expression and only outputs observations to
		the output data set if the condition is true.
		However, every observation is loaded into the PDV

	The WHERE statement tests the expression when the observation is
		in memory (in the input page buffer)
		Only observations for which the expression is true are loaded into the PDV

	Conclusion:  The output data set is created more efficiently using the WHERE statement

	However, the variables tested must exist in the input data set
		If they don't, then the IF statement must be used   *;


* ----------------------------- *
|   Comprehension Check 4.2.2   |
* ----------------------------- *
	Task:  Print the observations from the 'CanImpt.Laboratory_2015' data set
			which meet the following 2 conditions
			1)	Observations containing hematocrit values
			2)	In the date range from 9/16/2015 through 11/15/2015   *;
PROC PRINT DATA = CanImpt.Laboratory_2015;
	[add statement to filter the observations];
	RUN;


*	Illustration 10 - Comparing IF vs WHERE:  When the expression uses a new variable   *; RUN;

*	Goal:  Keep the subset of observations containing students who are underweight (BMI < 16)   *;
DATA	WORK.Underweight;
	SET	SASHelp.Class;

	BMI		= (Weight * 0.45) / (Height * 0.025)**2;
	FORMAT	BMI	4.1;

	IF	BMI < 16;

	RUN;

PROC PRINT DATA = WORK.Underweight;
	RUN;

*	Be aware of the following common programming errors!
	Wrong Approach 1:  Using a WHERE statement   *;
DATA	WORK.Underweight;
	SET	SASHelp.Class;

	BMI		= (Weight * 0.45) / (Height * 0.025)**2;
	FORMAT	BMI	4.1;

	WHERE	BMI < 16;

	RUN;

*	Wrong Approach 2:  Using an IF statement
	Can you determine why the data isn't filtered correctly?	*;
DATA	WORK.Underweight;
	SET	SASHelp.Class;

	IF	BMI < 16;

	BMI		= (Weight * 0.45) / (Height * 0.025)**2;
	FORMAT	BMI	4.1;

	RUN;

PROC PRINT DATA = WORK.Underweight;
	RUN;


*	Illustration 11 - Efficient Programming Example   *; RUN;

*	Goal:  Keep the subset of female students who are underweight
	The most efficient programming involves using both the IF and WHERE statements   *;
DATA	WORK.UnderweightF;
	SET	SASHelp.Class;
	WHERE	Sex	= 'F';

	BMI		= (Weight * 0.45) / (Height * 0.025)**2;
	FORMAT	BMI	4.1;

	IF	BMI < 16;

	RUN;

PROC PRINT DATA = WORK.UnderweightF;
	RUN;


*	Illustration 12 - Subsetting When Using a Procedure   *; RUN;

*	The IF statement is only a valid statement in the DATA step
	So procedures can only process subsets when using a WHERE statement (or data set option)
	Goal:  Print the data for the male students

	Correct Approach:  Use the WHERE statement   *;
PROC PRINT	DATA = SASHelp.Class;
	WHERE	Sex = 'M';
	RUN;

*	Incorrect Approach:  Using the IF statement   *;
PROC PRINT	DATA = SASHelp.Class;
	IF	Sex = 'M';
	RUN;

*	Inefficient Approach:
	This code will produce the desired results.  So why is it not good coding?
		1) The DATA step processes/evaluated every observation
			(a WHERE= data set option would only import the males)
		2) Observations are processed twice
			(first in the DATA step, then again in the PROC step)   *;
DATA	WORK.Class;
	SET	SASHelp.Class;
	IF	Sex = 'M';
	RUN;

PROC PRINT	DATA = WORK.Class;
	RUN;


*	Illustration 13 - Special WHERE Operators   *; RUN;

*	Special WHERE operators are not available for use with the IF statement

	The CONTAINS operator specifies a character string to search for
	Goal:  Create a subset of all 3rd basemen   *;
PROC PRINT DATA = SASHelp.Baseball;
	WHERE	Position CONTAINS '3';
	VAR	Name Team Position;
	RUN;

*	The BETWEEN AND operator specifies an inclusive range
	Using a compound WHERE statement   *;
PROC PRINT DATA = CanImpt.Laboratory_2015;
	WHERE	Lab_Test = 'WHITE BLOOD CELL COUNT'	AND
			Lab_Value	BETWEEN 8 AND 10	;
	RUN;

*	The IS MISSING and IS NOT MISSING operators
	Note:  The alias is "IS NULL"   *;
PROC PRINT DATA = CanImpt.Laboratory_2015;
	WHERE	Lab_Value IS MISSING;
	RUN;

*	The LIKE operator is used to match patterns
	The _ symbol represents a single space
	The % symbol represents any number of spaces
	Goal:  Print all subjects whose first name:
			1)	Begins with 'J', and
			2)	The 3rd letter is 'M'   *;
PROC PRINT DATA = CanImpt.Address;
	WHERE	First LIKE 'J_M%';
	RUN;

*	Goal:  Print all subjects whose last name ends with 'N'   *;
PROC PRINT DATA = CanImpt.Address;
	WHERE	Last LIKE '%N';
	RUN;




*	SECTION 4.2.3 - SUBSETTING VARIABLES IN A DATA SET   *; RUN;


*	Illustration 1 - Restricting the variables kept using DROP and KEEP statements   *; RUN;

*	Goal:  Keep all of the 'Career' baseball statistics

	Method 1:  KEEP the variables desired   *;
DATA	WORK.Career;
	SET	SASHelp.Baseball;
	KEEP	Name Cr:;
	RUN;

PROC PRINT DATA = WORK.Career (OBS = 5);
	RUN;

*	Method 2:  DROP the undesired variables   *;
DATA	WORK.Career;
	SET	SASHelp.Baseball;
	DROP	Team -- YrMajor League -- logSalary;
	RUN;

PROC PRINT DATA = WORK.Career (OBS = 5);
	RUN;


*	Illustration 2 - Multiple DROP or KEEP Statements are Permitted   *; RUN;

DATA	WORK.Career;
	SET	SASHelp.Baseball;
	DROP	Team -- YrMajor;
	DROP	League -- logSalary;
	RUN;

PROC PRINT DATA = WORK.Career (OBS = 5);
	RUN;


*	Illustration 3 - DROP and KEEP Statements may Both be Used   *; RUN;

*	Goal:  Keep all career statistics except walks   *;
DATA	WORK.Career;
	SET	SASHelp.Baseball;
	KEEP	Name Cr:;
	DROP	CrBB;
	RUN;

PROC PRINT DATA = WORK.Career (OBS = 5);
	RUN;


*	Illustration 4 - Using the DROP= and KEEP= Data Set Options   *; RUN;

*	The DROP= and KEEP= data set options limit the variables read into the PDV   *;
DATA	WORK.Career;
	SET	SASHelp.Baseball	(	KEEP	= Name Cr:	);
	RUN;

PROC PRINT DATA = WORK.Career (OBS = 5);
	RUN;


*	Illustration 5 - An Efficient Approach   *; RUN;

*	Goal:  Keep only the student's name and BMI   *;
DATA	WORK.BMI	(	KEEP	= Name BMI	); * Keeping only the desired variables *;

	SET	SASHelp.Class	(	KEEP	= Name Height Weight	); * Reading only needed variables into the PDV *;

	BMI		= (Weight * 0.45) / (Height * 0.025)**2;
	FORMAT	BMI	4.1;
	RUN;

PROC PRINT DATA = WORK.BMI;
	RUN;




*	SECTION 4.2.4 - ADDITIONAL MATERIAL   *; RUN;

*	None   *;




*	SECTION 4.2.5 - SOLUTIONS   *; RUN;


* ----------------------------- *
|   Comprehension Check 4.2.1   |
* ----------------------------- *
	Question:  Which observations will be read into the data set?   *;
DATA	WORK.Illus;
	SET	SASHelp.Cars	(FIRSTOBS = 301	OBS = 350);
	IF	_N_ >= 40;
	RUN;

*	The observations numbered 340-350 in the SASHelp.Cars data set will
		be included in the 'WORK.Illus' data set   *;


* ----------------------------- *
|   Comprehension Check 4.2.2   |
* ----------------------------- *
	Task:  Print the observations from the 'CanImpt.Laboratory_2015' data set
			which meet the following 2 conditions
			1)	Observations containing hematocrit values
			2)	In the date range from 9/16/2015 through 11/15/2015   *;
PROC PRINT DATA = CanImpt.Laboratory_2015;
	WHERE	Lab_Test = 'HEMATOCRIT' AND
			'16SEP2015'D <= Lab_Date <= '15NOV2015'D;
	RUN;




;	*';	*";	*/;	QUIT;	RUN;
*	End of Program   *; RUN;

